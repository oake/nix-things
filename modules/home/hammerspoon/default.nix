{
  config,
  lib,
  pkgs,
  ...
}:

let
  cfg = config.programs.hammerspoon;

  enabledScripts = lib.filterAttrs (_: v: v.enable) cfg.scripts;

  isValidName = n: builtins.match "^[A-Za-z0-9_-]+$" n != null;

  scriptPath =
    name: v:
    if v.text != null then pkgs.writeText "hammerspoon-script-${name}.lua" v.text else v.source;

  scriptsDir = pkgs.linkFarm "hammerspoon-scripts" (
    lib.mapAttrsToList (name: v: {
      name = "${name}.lua";
      path = scriptPath name v;
    }) enabledScripts
  );

  settingsLua = pkgs.writeText "hammerspoon-script-settings.lua" ''
    -- Generated by Home Manager
    return hs.json.decode([==[
      ${builtins.toJSON {
        global = cfg.settings;
        scripts = lib.mapAttrs (_: v: v.settings) enabledScripts;
      }}
    ]==])
  '';

  loaderLua = ''
    -- Generated by Home Manager
    local cfg = require("settings")
    local global = cfg.global or {}
    local perScript = cfg.scripts or {}

    local log = hs.logger.new('hm-loader','debug')

    local function merge(a, b)
      local out = {}
      for k, v in pairs(a) do
        out[k] = v
      end
      for k, v in pairs(b) do
        out[k] = v
      end
      return out
    end

    local function load_script(name)
      log.i("Loading script: " .. name)
      local mod = require("scripts." .. name)
      if type(mod) == "function" then
        mod(merge(global, perScript[name]))
      end
    end
  '';

  initLines = [
    loaderLua
  ]
  ++ lib.optional (cfg.preText != "") cfg.preText
  ++ map (n: ''load_script("${n}")'') (builtins.attrNames enabledScripts)
  ++ lib.optional cfg.scratchEnable ''require("scratch")''
  ++ lib.optional (cfg.postText != "") cfg.postText;

  initLua = pkgs.writeText "hammerspoon-init.lua" (lib.concatStringsSep "\n" initLines + "\n");

in
{
  imports = [
    ./scripts.nix
  ];

  options.programs.hammerspoon = {
    enable = lib.mkEnableOption "Hammerspoon config management";

    settings = lib.mkOption {
      type = lib.types.attrs;
      default = { };
      description = "Global freeform settings passed to all scripts.";
    };

    scripts = lib.mkOption {
      default = { };
      type = lib.types.attrsOf (
        lib.types.submodule {
          options = {
            enable = lib.mkEnableOption "Include this script in init.lua";

            source = lib.mkOption {
              type = lib.types.nullOr lib.types.path;
              default = null;
              description = "Path to a .lua file to link.";
            };

            text = lib.mkOption {
              type = lib.types.nullOr lib.types.lines;
              default = null;
              description = "Inline Lua content for this script.";
            };

            settings = lib.mkOption {
              type = lib.types.attrs;
              default = { };
              description = "Per-script settings (override global settings keys).";
            };
          };
        }
      );
    };

    preText = lib.mkOption {
      type = lib.types.lines;
      default = "";
      description = "Lua prepended at the beginning of init.lua.";
    };

    postText = lib.mkOption {
      type = lib.types.lines;
      default = "";
      description = "Lua appended at the end of init.lua.";
    };

    scratchEnable =
      lib.mkEnableOption "creation and inclusion of scratch.lua for quick experiments"
      // {
        default = true;
      };
  };

  config = lib.mkIf cfg.enable {
    assertions =
      (lib.mapAttrsToList (name: v: {
        assertion = isValidName name;
        message = "programs.hammerspoon.scripts.${name}: name must match ^[A-Za-z0-9_-]+$";
      }) cfg.scripts)
      ++ (lib.mapAttrsToList (name: v: {
        assertion = (v.source != null) != (v.text != null);
        message = "programs.hammerspoon.scripts.${name}: set exactly one of `source` or `text`.";
      }) cfg.scripts);

    home.packages = [
      pkgs.hammerspoon
    ];

    home.file.".hammerspoon/scripts".source = scriptsDir;
    home.file.".hammerspoon/init.lua".source = initLua;
    home.file.".hammerspoon/settings.lua".source = settingsLua;

    home.activation.hammerspoonScratch = lib.mkIf cfg.scratchEnable (
      lib.hm.dag.entryAfter [ "writeBoundary" ] ''
        mkdir -p "$HOME/.hammerspoon"
        if [ ! -e "$HOME/.hammerspoon/scratch.lua" ]; then
          cat > "$HOME/.hammerspoon/scratch.lua" <<'EOF'
        -- This file is not managed by Home Manager and is meant for quick experiments / to draft scripts.
        EOF
        fi
      ''
    );
  };
}
